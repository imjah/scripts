main         := "ttv.py"
menu         := "ttvmenu"
venv         := "./.venv"
vbin         := venv / "bin"
vpip         := vbin / "pip"
vpython      := vbin / "python"
vpytest      := vbin / "pytest"
vpyinstaller := vbin / "pyinstaller"

setup:
	python3 -m venv {{venv}}
	{{vpip}} install -r requirements.txt

build:
	{{vpyinstaller}} --onefile {{main}}

clean:
	rm -rf *.spec .pytest_cache/ {{venv}} __pycache__/ build/ dist/

pip *ARGS:
	{{vpip}} {{ARGS}} && {{vpip}} freeze > requirements.txt

python *ARGS:
	{{vpython}} {{ARGS}}

run *ARGS:
	{{vpython}} {{main}} {{ARGS}}

test *ARGS:
	{{vpytest}} {{ARGS}}

install:
	cp -t "{{executable_directory()}}" "dist/{{without_extension(main)}}" {{menu}}

uninstall:
	rm -f "{{executable_directory() / without_extension(main)}}" \
	      "{{executable_directory() / menu}}"
