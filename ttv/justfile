exec    := 'ttv'
menu    := 'ttvmenu'
main    := 'ttv.py'
reqs    := 'requirements.txt'
venv    := './.venv'
vpip    := venv / 'bin/pip'
vpython := venv / 'bin/python'
bin     := executable_directory()
data    := data_local_directory() / exec

setup:
	python3 -m venv "{{venv}}"
	{{vpip}} install -r "{{reqs}}"

clean:
	rm -rf "{{venv}}"

install: setup
	mkdir -p "{{bin}}" "{{data}}"
	cp -t "{{bin}}" "{{exec}}" "{{menu}}"
	cp -rt "{{data}}" "{{venv}}" "{{main}}"

uninstall:
	rm -rf "{{bin / exec}}" "{{bin / menu}}" "{{data}}"

pip *args:
	{{vpip}} {{args}} && \
	{{vpip}} freeze > "{{reqs}}"

run *args:
	{{vpython}} "{{main}}" {{args}}
